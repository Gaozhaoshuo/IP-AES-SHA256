// ---------------------------------------------------------------------------//
// The confidential and proprietary information contained in this file may
// only be used by a person authorised under and to the extent permitted
// by a subsisting licensing agreement from SiliconThink.
//
//      (C) COPYRIGHT SiliconThink Limited or its affiliates
//                   ALL RIGHTS RESERVED
//
// This entire notice must be reproduced on all copies of this file
// and copies of this file may only be made by a person if such person is
// permitted to do so under the terms of a subsisting license agreement
// from SiliconThink or its affiliates.
// ---------------------------------------------------------------------------//

//----------------------------------------------------------------------------//
// File name    : apb_ms_model.v
// Author       : sky@SiliconThink
// Email        : 
// Project      :
// Created      : 
// Copyright    : 
//
// Description  : 
// 1: APB master inf model.
//----------------------------------------------------------------------------//
`timescale 1ns / 10ps

`include "sha_sim_def.v"

module apb_ms_model(
	//--- APB configure inf
	psel			,
	penable			,
	paddr			,
	pwrite			,
	pwdata			,
	pready			,
	prdata			,

    intr            ,

    clk				,
    rstn			 
);

parameter   IFNAME    = "C:/Gaozs/Prj/SecureLink/Src/TESTCASE/inout_sha256.bin";
parameter   SHA_CFG_BASE = 'd0;


input   wire			clk, rstn		;


//--- APB configure inf
output	reg				psel			;
output	reg				penable			;
output	reg		[31:0]	paddr			;	//byte addr
output	reg				pwrite			;
output	reg		[31:0]	pwdata			;
input	wire			pready			;
input	wire	[31:0]	prdata			;

input   wire            intr            ;

//--- 1: apb w/r task

task apb_write;
input	[31:0]	addr	;
input	[31:0]	wdata	;
begin
	@(posedge clk); #1;
	psel = 1; pwrite = 1; paddr = addr; pwdata = wdata;
	@(posedge clk); #1;
	penable = 1;
	
	@(negedge clk);
	while(!pready) begin
		@(negedge clk);
	end

	@(posedge clk); #1;
	psel = 0; penable = 0;
end
endtask

task apb_read;
input	[31:0]	addr	;
output	[31:0]	rdata	;
begin	
	@(posedge clk); #1;
	psel = 1; pwrite = 0; paddr = addr;
	@(posedge clk); #1;
	penable = 1;
	
	@(negedge clk);
	while(!pready) begin
		@(negedge clk);
	end
	rdata = prdata;

	@(posedge clk); #1;
	psel = 0; penable = 0;
end
endtask


//--- 2: SHA cfg initial ctrl ---//

task fget_32b;
input   [31:0]  fp      ;
output  [31:0]  rdata   ;
begin
    rdata[0*8 +: 8]  = $fgetc(fp);
    rdata[1*8 +: 8]  = $fgetc(fp);
    rdata[2*8 +: 8]  = $fgetc(fp);
    rdata[3*8 +: 8]  = $fgetc(fp);
end
endtask

integer             in_fp       ;
reg     [31:0]      tmp32       ;
reg     [7:0]       tmp8        ;
reg     [31:0]      rdata       ;

reg     [31:0]      in_byte_len ;   //cnt from 1
reg     [31:0]      bulk_num    ;
reg     [31:0]      in_data_base;   //4B aligned

reg     [31:0]      i, j        ;

initial begin

    in_fp = $fopen(IFNAME, "rb");
    if(in_fp == 0) begin
        $display("Sim Erro: File %s doesn't exist. Which is needed by %m .", IFNAME);
        $finish();
    end

	psel		= 0;
	penable		= 0;
	paddr		= 0;
	pwrite		= 0;
	pwdata		= 0;
    #10;

	@(posedge rstn);
    repeat(10) @(posedge clk);

    //--- get head info
    fget_32b(in_fp, bulk_num);

    //apb_write(SHA_CFG_BASE + 4*4, `IN_DATA_BASE);

    //--- print sim case info
    $display("Info: sim case info:");
    $display("Total test bulk number: %8h.", bulk_num);
    $display("");

    //--- sha256 of each package
    for(i=0; i<bulk_num; i=i+1) begin
        $display("Info: sim for package: %d.", i);

        in_data_base = 4096 - (64 + i)*4;   //test cross 4KB boundary

        fget_32b(in_fp, in_byte_len);
        apb_write(SHA_CFG_BASE + 4*2, (in_byte_len-1));
        apb_write(SHA_CFG_BASE + 4*1, in_data_base);

        //--- write input test to sys_mem
        for(j=0; j<in_byte_len; j=j+1) begin
            tmp8 = $fgetc(in_fp);
            `SYS_MEM_PATH[in_data_base + j] = tmp8;
        end

        //--- start the cal of a bulk
        apb_write(SHA_CFG_BASE + 4*0, 32'h1);

        //--- wait cal end and check result
        @(posedge intr);
        apb_write(SHA_CFG_BASE + 4*3, 32'h0);

        repeat(2) @(posedge clk);
        for(j=0; j<8; j=j+1) begin
            fget_32b(in_fp, tmp32);
            apb_read(SHA_CFG_BASE + (8+j)*4 , rdata);

            if(tmp32 !== rdata) begin
                $display("Error: sim fail.");
                $display("Fail at test bulk of %8x, 32bit result location %4x .", i, j);
                $display("Ref is %8x, DUT out is %8x .", tmp32, rdata);

                $fclose(in_fp);
                // $fclose(tb.u_sha256_trc.fp);
                $finish();
            end
        end
    end

    repeat(10) @(posedge clk); #1;
    $display("OK: sim pass.");
    $fclose(in_fp);
    // $fclose(tb.u_sha256_trc.fp);
    $finish();

end

endmodule

